// A frontend that allows to switch version of the role
import {entries, repr, pretty, is_string, reversed} from '../util/object'
import pending_actions from '../stores/actions'
import {refresher, json} from '../middleware/request'
import {execute} from '../util/action'
import {is_leader} from '../util/status'
import {total_processes} from '../util/schedule'
import {show_leaders} from '../util/leaders.khufu'
import {logs} from './logs.khufu'
import {value, set, init} from '../stores/simple'
import {url_query, smart_query} from '../util/routing'
import {filter_versions} from '../util/version'


style:
  .rotate
    animation-name: rotateThis
    animation-duration: .5s
    animation-iteration-count: infinite
    animation-timing-function: linear
  .panel
    max-width: 40ex


view main(role, role_name, {role_state, schedule, system_status}):
  <h2> "Status Info"
  <table.table.table-hover>
    <thead>
      <tr>
        <th> "Property"
        <th> "Value"
    <tbody>
      <tr>
        <td> "Version"
        <td>
          role_state.version
    if role.info:
      <tbody>
        for [name, value] of entries(role.info) key name:
          <tr>
            <td>
              name
            <td>
              value
  <h2> `Processes [${total_processes(schedule, role_name)}]`
  <table.table.table-hover>
    <thead>
      <tr>
        <th> "Host"
        <th> "Process"
        <th> "Image"
        <th> "Num"
    for [host, node] of entries(schedule.nodes) key host:
      let hrole = node.roles[role_name]
      <tbody>
        if hrole:
          for [kind, props] of entries(hrole.daemons) key kind:
            <tr>
              <td>
                host
              <td>
                kind
              <td>
                props.image
              <td>
                props.instances
  logs(role_name)
  if is_leader(system_status):
    <div>
      store @actions = pending_actions | refresher
        | json('/v1/pending_actions')
      <h2> "Available Versions"
      store @trunc = value | url_query('trunc') | init('')
      store @filter = value | smart_query('versions') | init('')
      <div.input-group.panel>
        <div.input-group-btn>
          <button.btn.btn-default.active?(@trunc == 'no')>
            link {click} set(@trunc == 'no' ? '' : 'no') -> @trunc
            "Show All"
        <input.form-control placeholder=`Filter Versions` value=@filter>
          link {input} set(this.value) -> @filter
        if @filter:
          <div.input-group-btn>
            <button.btn.btn-default>
              link {click} set('') -> @filter
              <span.glyphicon.glyphicon-remove>
      <table.table.table-hover>
        <thead>
          <tr>
            <th> "Version"
        <tbody>
          let all = filter_versions(role.versions or [], @filter)
          let shown = @trunc == 'no' and all or all.slice(0, 20)
          for version of shown:
            <tr>
              <td>
                "Switch to "
                <button.btn.btn-default>
                  link {click} execute(
                    {"button": {"version": version, "role": role_name}}
                  ) -> @actions
                  version
                  for [id, act] of entries(@actions) key id:
                    if act.button.version == version:
                      " "
                      <span.glyphicon.glyphicon-refresh.rotate>
      if role.frontend.allow_stop:
        <h2>
          if role_state.running:
            "Running "
            <button.btn.btn-default>
              link {click} execute(
                {"button": {"stop": true, "role": role_name}}
              ) -> @actions
              "Stop"
              for [id, act] of entries(@actions) key id:
                if act.button.stop:
                  " "
                  <span.glyphicon.glyphicon-refresh.rotate>
          else:
            "Stopped "
            <button.btn.btn-default>
              link {click} execute(
                {"button": {"start": true, "role": role_name}}
              ) -> @actions
              "Start"
              for [id, act] of entries(@actions) key id:
                if act.button.start:
                  " "
                  <span.glyphicon.glyphicon-refresh.rotate>
  else:
    show_leaders("Actions are Only Available on Leader", `/role/${role_name}`,
      system_status)
